@ECHO OFF
CHCP 65001 > nul

SET PREFIX=InfDev
SET REPOSITORY_NAME=SKit.LiqPay.SDK

SET ARCHIVE_LOCAL_ROOT=\!GITARC\
SET ARCHIVE_EXTERNAL_ROOT=f:\Res\OneDrive\!GITARC\

SET ARCH_LOCAL_FOLDER=%ARCHIVE_LOCAL_ROOT%%PREFIX%\%REPOSITORY_NAME%\
SET ARCH_EXTERNAL_FOLDER=%ARCHIVE_EXTERNAL_ROOT%%PREFIX%\%REPOSITORY_NAME%\
if not exist %ARCH_LOCAL_FOLDER% mkdir %ARCH_LOCAL_FOLDER%
if not exist %ARCH_EXTERNAL_FOLDER% mkdir %ARCH_EXTERNAL_FOLDER%

SET YYYYMMDD_HHMMSS=%date:~6,4%%date:~3,2%%date:~0,2%_%time:~0,2%%time:~3,2%%time:~6,2%
IF " " == "%YYYYMMDD_HHMMSS:~9,1%" SET YYYYMMDD_HHMMSS=%YYYYMMDD_HHMMSS: =0%
IF " " == "%YYYYMMDD_HHMMSS:~6,1%" SET YYYYMMDD_HHMMSS=%YYYYMMDD_HHMMSS: =0%

echo "Добавление в индекс текущих файлов ..."
git add --all
echo "Зафиксировать изменения ..."
git commit -m "Step %YYYYMMDD_HHMMSS%"

SET FILENAME=%PREFIX%!%REPOSITORY_NAME%!%YYYYMMDD_HHMMSS%.zip
SET ARCH_LOCAL_PATH=%ARCH_LOCAL_FOLDER%%FILENAME%

echo "Архивация репозитария в %ARCH_LOCAL_PATH% ..."
git archive -o %ARCH_LOCAL_PATH% HEAD

echo "Копирование архива на внешний носитель в %ARCH_EXTERNAL_FOLDER% ..."
copy %ARCH_LOCAL_PATH% %ARCH_EXTERNAL_FOLDER%
